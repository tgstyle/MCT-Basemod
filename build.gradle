buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

ext.configFile = file "../common/common-1.12.properties"
configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	ext.common = new ConfigSlurper().parse prop
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
	maven {
		name = 'CoFH Maven'
		url = 'http://maven.covers1624.net/'
	}
	maven {
		name = 'ChickenBones Maven'
		url = "http://chickenbones.net/maven/"
	}	
	maven {
		name = 'JEI Maven'
		url = 'http://dvs1.progwml6.com/files/maven'
	}
	maven {
		name = 'HWYLA Maven'
		url = 'http://tehnut.info/maven'
	}
	maven {
		name = 'Immersive Maven'
		url = 'http://blamejared.com/maven'
	}
	maven {
		name = 'tterrag Maven'
		url = 'http://maven.tterrag.com/'
	}	
}


version = common.mcversion + "-" + modversion+ "." + modsubversion
group = modgroup
archivesBaseName = modarchivename

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = common.mcversion + "-" + common.forgeversion
	mappings = common.mcp_mappings
	runDir = "run"
	replace '${version}', modversion+ "." + modsubversion
}

dependencies {
	compile "mezz.jei:jei_${common.mcversion}:${common.jei_version}"

	deobfCompile "cofh:RedstoneFlux:${common.mcbaseversion}-${common.redf_version}:deobf"
	deobfCompile "cofh:CoFHCore:${common.mcversion}-${common.cofh_version}:deobf"
	deobfCompile "cofh:CoFHWorld:${common.mcversion}-${common.cwor_version}:deobf"
	deobfCompile "cofh:ThermalFoundation:${common.mcversion}-${common.thfo_version}:deobf"
	deobfCompile "cofh:ThermalExpansion:${common.mcversion}-${common.thex_version}:deobf"
	deobfCompile "codechicken:CodeChickenLib:${common.mcversion}-${common.cclo_version}:deobf"
	compileOnly "mcjty.theoneprobe:TheOneProbe-${common.mcbaseversion}:${common.mcbaseversion}-${common.top_version}"

	compileOnly "slimeknights:TConstruct:${common.mcversion}-${common.tcon_version}"
	compileOnly "slimeknights.mantle:Mantle:${common.mcbaseversion}-${common.mant_version}"

}

processResources {
	inputs.property "version", project.modversion + "." + project.modsubversion
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
				
		expand "version":project.modversion + "." + project.modsubversion, "mcversion":project.minecraft.version
	}
		
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}